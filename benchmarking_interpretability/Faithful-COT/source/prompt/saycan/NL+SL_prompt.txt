; I'm a robot. Given a user query in Natural Language, my task is to parse it into a goal in Planning Domain Definition Language (PDDL).

; Type definition: the hierarchy of object types.
(:types
	robot location item - object 	; object includes robot, location, and item
	snack drink tool - item			; item includes snack, drink, and tool
	fruit non-fruit - snack 		; snack includes fruit and non-fruit
	soda non-soda - drink 			; drink includes soda and non-soda
)

; Object definition: specific objects of each object type defined above.
(:objects
	me - robot 																		; me, the robot
	counter table user trash bowl initial - location								; all locations
	apple orange - fruit 															; all fruit snacks
	kettle-chips multigrain-chips jalapeno-chips rice-chips energy-bar - non-fruit 	; all non-fruit snacks
	seven-up coke lime-soda grapefruit-soda pepsi - soda 							; all soda drinks
	tea redbull water - non-soda 													; all non-soda drinks
	sponge - tool 																	; all tools
)

; Predicate definition: relations between objects.
(:predicates
  (at ?obj - object ?loc - location)	; an object is at a location
  (inventory ?r - robot ?itm - item)  	; an item is in the robot's inventory
  (visited ?loc - location)			    ; a location is visited
  (is-caffeinated ?i - item)			; an item is caffeinated
  (is-salty ?i - item)				    ; an item is salty
  (is-sweet ?i - item)				    ; an item is sweet
  (is-spicy ?i - item)				    ; an item is spicy
  (is-clear ?i - item)				    ; an item is clear in color
  (is-refreshing ?i - item)			    ; an item is refreshing
)

; When parsing the user query into a goal, I can only use the types, objects, and predicates defined above, and the following logical operators:
; (and (statement1) (statement2) ...) ; all the statements are true
; (or (statement1) (statement2) ...) ; at least one of the statements are true
; (not (statement)) ; the statement is not true
; (forall (type specification) (statement)) ; for all objects of the specified type, the statement is true
; (exists (type specification) (statement)); there exists an object of the specified type such that the statement is true
; (= (object1) (object2)) ; object1 and object2 are equivalent
; (imply (statement1) (statement2)) ; if statement1 is true, then statement2 is true

; I begin parsing now.

User query: Can you go to the counter, the trash can, and then to me?
Goal in PDDL:
(:goal
	; I need to visit all three locations
	(and
		; first visit the counter
		(visited counter)
		; then visit the trash can
		(visited trash)
		; finally go to the user
		(at me user)
	)
)

User query: How would you compost the redbull and bring me a cleaning tool?
Goal in PDDL:
(:goal
	; I need to do both things
	(and
		; first compost the redbull
		(at redbull trash)
		; then bring the user a sponge as a cleaning tool
		(at sponge user)
	)
)

User query: Bring me something not sweet to eat.
Goal in PDDL:
(:goal
	; I need to find a snack
	(exists (?s - snack)
		; it has to satisfy the following conditions
		(and
			; the snack must not be sweet
			(not (is-sweet ?s))
			; bring it to the user
			(at ?s user)
		)
	)
)

User query: Can you lift a fruit and hold it in your gripper?
Goal in PDDL:
(:goal
	; I need to choose one fruit
	(exists (?f - fruit)
		; lift it and hold it in my gripper
		(inventory me ?f)
	)
)

User query: Move all caffeinated sodas from the table to the counter.
Goal in PDDL:
(:goal
	; I need to deal with every soda that satisfies the following conditions
	(forall (?s - soda)
		; when
		(imply
			; the soda is caffeinated
			(is-caffeinated ?s)
			; move it
			(and
                ; from the table
                (not (at ?s table))
                ; to the counter
                (at ?s counter)
            )
		)
	)
)

User query: I don't like oranges. Can you bring me two different things to eat?
Goal in PDDL:
(:goal
	; I need to bring the user two snacks
	(exists (?s1 - snack ?s2 - snack)
		; they need to satisfy all the following conditions
		(and
			; the first snack is brought to the user
			(at ?s1 user)
			; the second snack is brought to the user
			(at ?s2 user)
			; the two snacks are different
			(not (= ?s1 ?s2))
			; the first snack is not an orange
			(not (= ?s1 orange))
			; the second snack is not an orange
			(not (= ?s2 orange))
		)
	)
)

User query: Set up my dinner at the table: a bag of chips, a fruit, and a drink that is not a pepsi or a coke.
Goal in PDDL:
(:goal
	; I need to bring all three things to the table
	(and
		; bring a bag of chips
		(or
			; kettle-chips are chips
			(at kettle-chips table)
			; multigrain-chips are chips
			(at multigrain-chips table)
			; jalapeno-chips are chips
			(at jalapeno-chips table)
			; rice-chips are chips
			(at rice-chips table)
		)
		; bring a fruit
		(exists (?f - fruit)
			; bring any fruit to the table
			(at ?f table)
		)
		; bring a drink
		(exists (?d - drink)
			; that satisfies all these conditions
			(and
				; it's not a pepsi
				(not (= ?d pepsi))
				; it's not a coke
				(not (= ?d coke))
				; bring it to the table
				(at ?d table)
			)
		)
	)
)